Sub wishlists <<EOS
sub {
	my $page = shift;
	my ($name, @args) = split(/\//, $page);
	my ($wishlist_code, $wishlist_name);

	unless ($Session->{logged_in}) {
		$Tag->warnings(errmsg('Please login before adding a product to the wishlist.'));
		$CGI->{mv_nextpage} = 'login';
		$Scratch->{login_target} = '';
		$Scratch->{login_bounce} = $page;
		return 1;
	}

	if ($args[1]) {
		# select wishlist by code or name
		if ($args[1] =~ /^(\d+)$/) {
			$wishlist_code = $args[1];
			$wishlist_name = $Tag->wishlist({function => 'name',
				code => $wishlist_code});
			$Tag->attribute('set', 'wishlist', 'code', $args[1]);
			$Tag->attribute('set', 'wishlist', 'name', $wishlist_name);
		}
		else {
			$wishlist_name = $args[1];
			$Tag->attribute('set', 'wishlist', 'name', $args[1]);
		}
	}
	
	if ($args[0] eq 'create') {
		$Values->{sku} = $args[1];
		$Tag->wishlist('create', $Values->{sku});
		$CGI->{mv_nextpage} = 'wishlists/create';
		return 1;
	}

	# whether to move stuff from wishlist to cart
	if ($CGI->{movetocart}) {
		for (@{$CGI_array->{sku}}) {
			$Tag->addtocart($_, 1);
			$Tag->wishlist({function => 'remove',
							sku => $_,
							name => $wishlist_name,
							code => $wishlist_code});
		}
		$CGI->{mv_nextpage} = $Config->{Special}->{order};
		return 1;
	}

	# whether to copy stuff from wishlist to cart
	if ($CGI->{addtocart}) {
		for (@{$CGI_array->{sku}}) {
			$Tag->addtocart($_, 1);
		}
		$CGI->{mv_nextpage} = $Config->{Special}->{order};
		return 1;
	}

	# whether to delete stuff from wishlist
	if ($CGI->{remove}) {
		$Tag->wishlist({function => 'remove',
						sku => $CGI->{remove},
						name => $wishlist_name,
						code => $wishlist_code});
	}

	# whether to update wishlist
	if ($CGI->{update}) {
		for (my $i = 0; $i < @{$CGI_array->{sku}}; $i++) {
			my %data = (function => 'update',
						sku => $CGI_array->{sku}->[$i],
						name => $wishlist_name,
						code => $wishlist_code);

			for my $f (split(/\s*,\s*/, $Variable->{WISHLISTS_PRODUCTS_FIELDS})) {
				$data{$f} = $CGI_array->{$f}->[$i];
			}

			$Tag->wishlist(\%data);
		}
	}

	# whether to finalize wishlist
	if ($CGI->{finalize}) {
		$Tag->wishlist({function => 'status', name => $wishlist_name,
			code => $wishlist_code, status => 'final'});
	}

	# show wishlist for the current user
	if ($args[0] eq 'carts') {
		$CGI->{mv_nextpage} = 'wishlists/carts';
	}
	else {
		$CGI->{mv_nextpage} = 'wishlists/list';
	}

	return 1;	
}
EOS
